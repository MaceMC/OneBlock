plugins {
    id 'java'
    id("com.gradleup.shadow") version "8.3.5"
}

group = "org.macemc"
version = '0.0.1'
def author = 'tooobiiii'

repositories {
    mavenCentral()

    maven { url = "https://repo.papermc.io/repository/maven-public/" }
    maven { url = "https://jitpack.io" }
    maven { url = "https://bitbucket.org/kangarko/libraries/raw/master" }
}

dependencies {
    compileOnly("io.papermc.paper:paper-api:1.21.1-R0.1-SNAPSHOT")

    implementation("com.github.kangarko:Foundation:6.9.12")

    compileOnly("org.projectlombok:lombok:1.18.34")

    annotationProcessor("org.projectlombok:lombok:1.18.34")
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

tasks.named('shadowJar') {
    dependencies {
        include(dependency('org.apache.logging.log4j:log4j-core:2.11.1'))
    }
}

processResources {
    def props = [version: version, projectName: project.name, author: author]
    inputs.properties props
    filteringCharset 'UTF-8'

    filesMatching('**/*.yml') {
        expand props
    }
}